import org.apache.avro.tool.SpecificCompilerTool

import java.time.Duration

buildscript {
	dependencies {
		// Add the Avro code generation to the build dependencies so that it can be used in a Gradle task.
		classpath 'org.apache.avro:avro-tools:1.12.0'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id "com.avast.gradle.docker-compose" version "0.17.2"
	id 'com.gorylenko.gradle-git-properties' version '2.4.0'
	id "com.github.davidmc24.gradle.plugin.avro" version '1.9.1'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo kafka and lib gatling for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
	gradlePluginPortal()
}

ext {
	set('springCloudVersion', "2025.0.0")
	set("kafkaVersion", "3.9.1")
}

dependencies {
	implementation(
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.cloud:spring-cloud-starter-config',
			'org.springframework.cloud:spring-cloud-starter-bootstrap',
			"org.springframework.boot:spring-boot-starter-actuator:3.5.3",

			"pl.project13.maven:git-commit-id-plugin:4.9.10",

			"org.springframework.boot:spring-boot-starter-webflux:3.5.4",

			"org.apache.avro:avro:1.12.0",
			"org.apache.avro:avro-tools:1.12.0",

			//kafka
			'org.springframework.kafka:spring-kafka:3.3.8',
			"org.apache.kafka:kafka-clients:${kafkaVersion}",
			"org.apache.kafka:kafka-streams:${kafkaVersion}",


			"org.projectlombok:lombok:1.18.32"
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test',
			"org.junit.jupiter:junit-jupiter-api:5.12.2",
	)

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	annotationProcessor(
			"org.projectlombok:lombok:1.18.32"
	)
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

apply plugin: 'docker-compose'

gitProperties {
	keys = ['git.branch','git.commit.id','git.commit.time']
}


def avroSchemasDir = "./src/main/resources/avro"
def avroCodeGenerationDir = "./build/generated/avro"

// Add the generated Avro Java code to the Gradle source files.
sourceSets.main.java.srcDirs += [avroCodeGenerationDir]


tasks.register("generateAvro") {
	// Define the task inputs and outputs for the Gradle up-to-date checks.
	inputs.dir(avroSchemasDir)
	outputs.dir(avroCodeGenerationDir)
	// The Avro code generation logs to the standard streams. Redirect the standard streams to the Gradle log.
	logging.captureStandardOutput(LogLevel.INFO);
	logging.captureStandardError(LogLevel.ERROR)
	doLast {
		// Run the Avro code generation.
		new SpecificCompilerTool().run(System.in, System.out, System.err, List.of(
				"-encoding", "UTF-8",
				"-string",
				"-fieldVisibility", "private",
				"-noSetters",
				"schema", "$projectDir/$avroSchemasDir".toString(), "$projectDir/$avroCodeGenerationDir".toString()
		))
	}
}

tasks.named('test', Test.class) {
	include "**/unit/**"

	testLogging {
		events 'passed', 'skipped', 'failed'
	}
	useJUnitPlatform()
}

tasks.register('startTestEnvironment') {
	dependsOn += ['composeUp']
}

tasks.register('stopTestEnvironment') {
	dependsOn += ['composeDown']
}

tasks.register('componentTest', Test.class) {
	minHeapSize = "1024m"
	maxHeapSize = "2048m"

	include "**/component/**"
	description = "Run component test"
	dependsOn tasks.startTestEnvironment

	//will docker-compose down
	finalizedBy tasks.stopTestEnvironment

	testLogging {
		events 'passed', 'skipped', 'failed'
	}
	useJUnitPlatform()
}

tasks.register('startApp', DefaultTask.class) {
	doFirst {
		'./gradlew bootRun'.execute()
	}
}

tasks.register('stopApp', DefaultTask.class) {
	doFirst {
		'./gradlew stop'.execute()
	}
}

tasks.register('blackboxTest', Test.class) {
	include "**/blackbox/**"
	description = "Run blackbox test"
	dependsOn tasks.startApp
	mustRunAfter tasks.componentTest

	finalizedBy tasks.stopApp
	testLogging {
		events 'passed', 'skipped', 'failed'
	}
	useJUnitPlatform()
}

// config for plugin. Do not use tasks.register
dockerCompose {
	useComposeFiles = ['src/test/resources/docker/docker-compose.yml']
	dockerComposeStopTimeout = Duration.ofSeconds(120)
	checkContainersRunning = false
	executable = "docker-compose"
	dockerExecutable = "docker"
	projectName = "kafkapilot"
}

tasks.withType(JavaCompile).configureEach {
	// Make Java compilation tasks depend on the Avro code generation task.
	dependsOn += ['generateAvro']
}

tasks.build.dependsOn += ['test']